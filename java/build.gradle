project.ext.set("archivesBaseName", "IndicKeyboard");

allprojects {
    repositories {
        google()
        jcenter()
    }
}

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
    }
}

apply plugin: 'com.android.application'

dependencies {
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation project(':ime')
}

// Used in calculating version code for each APK in splits
ext.versionCodeOffsets = ['arm64-v8a': 0, 'armeabi-v7a': 1]

import com.android.build.OutputFile

android {
    compileSdkVersion 28
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId "in.androidtweak.inputmethod.indic"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 48
        versionName "3.1"
    }

    splits {
      abi {
        enable true
        universalApk false
        reset()
        include "armeabi-v7a", "arm64-v8a"
      }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['../java-overridable/src', 'src', '../common/src', '../inputmethodcommon/java']
            jniLibs.srcDirs = ['jniLibs']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        androidTest.setRoot('tests')
    }

    aaptOptions {
        noCompress ".dict"
    }

    signingConfigs {
        release {
            storeFile file(String.valueOf(System.getenv("INDICPRIME_KEYSTORE")))
            storePassword System.getenv("INDICPRIME_KEYSTORE_PASSWORD")
            keyAlias System.getenv("INDICPRIME_KEY_ALIAS")
            keyPassword System.getenv("INDICPRIME_KEY_PASSWORD")
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }

    android.applicationVariants.all { variant ->
      variant.outputs.each { output ->
        // Uses a hardcoded lookup instead of loop index because in F-droid build
        // we overwrite splits and make only one output at a time.
        output.versionCodeOverride =
          variant.versionCode - project.ext.versionCodeOffsets.get(output.getFilter(OutputFile.ABI))
      }
    }
}
